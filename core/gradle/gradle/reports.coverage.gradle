// Coverage (JaCoCo) ////////////////////////////////////////////////////////
// Run as: gradle testGlobal jacocoRootReport

buildscript {
	repositories {
		mavenCentral()
	}
}

apply plugin: "jacoco"

subprojects {
	apply plugin: "jacoco"

	jacoco {
		toolVersion = "0.7.1.201405082137"
	}
	test {
		jacoco {
			append = true
			classDumpFile = file("${rootProject.buildDir}/jacoco/classpathdumps")
			destinationFile = file("${rootProject.buildDir}/jacoco/tests.exec")
		}
	}


	jacocoTestReport {
		additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
		sourceDirectories = files(sourceSets.main.allSource.srcDirs)
		classDirectories =  files(sourceSets.main.output)

		reports {
			html.enabled = true
			xml.enabled = true
			csv.enabled = false
		}
	}
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
	dependsOn = subprojects.test
	additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
	sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories =  files(subprojects.sourceSets.main.output)
	executionData = files(subprojects.jacocoTestReport.executionData)

	reports {
		html.enabled = true
		html.destination "${rootProject.buildDir}/reports/coverage"

		xml.enabled = true
		xml.destination "${rootProject.buildDir}/reports/coverage/coverage.xml"

		csv.enabled = false
	}   
}

